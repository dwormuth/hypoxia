C   ./ i 0
$STORAGE:2
      SUBROUTINE CHART2 (ISUB,NKNOWN)
C
C     'CONDENSED' CHART SUBROUTINE FOR HYPOXIA.  CHARTS 19,20,24-26.
C
      REAL I, J, IDEAL C
      CHARACTER*4 NAME
      COMMON ICOM1(176), NAME(7),ISTUDY,IDATE(3),IFLAG(75),NVAR,NTABLE,
     A   PARAM(75),  ICOM2(96)
      EQUIVALENCE (A PCO2,PARAM(4)), (PAO2,PARAM(7)), (A PO2,PARAM(9)),
     A   (A PH,PARAM(10)), (A SO2,PARAM(11)), (O2 CAP,PARAM(13)),
     B   (VISCOS,PARAM(14)),
     C   (VO2,PARAM(15)), (Q,PARAM(16)), (V SO2,PARAM(17)),
     D   (V PH,PARAM(18)), (TEMP,PARAM(21)), (HCT,PARAM(30)),
     E   (HGB,PARAM(33)), (A O2 TOT,PARAM(36)), (QB,PARAM(48)),
     F   (QT,PARAM(49)), (XV PO2,PARAM(50)), (VCO2,PARAM(53)),
     G   (SHUNT,PARAM(57)), (A CO2 T,PARAM(58)), (XV CO2 T,PARAM(59)),
     H   (V PCO2,PARAM(60))
C
      DATA H,I,J,P,QQ /3.23431 E-6, -4.46082 E-4, 2.25409 E-2,
     A   -4.79044 E-1, 4.36567/
      DATA  F,R,S /0.003,43.0,2.3/
      DATA G,BRPCT /98.0,0.15/
C
      ISUB18 = ISUB - 18
      GO TO (1900,2000,9900,9900,9900,2400,2500,2600), ISUB18
C
C***************************************
C     SUBROUTINE BLOOD FLOW   CHART 19
C     BLOOD FLOW EQUATIONS FROM MILHORN, ET. AL., BIOPHYS. J.,
C        5.27 (1965).
C
C     CALCULATE BRAIN BLOOD FLOW (QB) FROM GIVEN EMPIRICAL EQUATIONS.
C        TISSUE BLOOD FLOW (QT) FROM QB & Q.
C     MODIFICATIONS OF MILHORN - 1)  MILHORN USES ALV PO2 & RATIO OF
C        'NORMAL' ART PO2 TO 'NORMAL' ALV PO2 TO CALC. A USEABLE ART
C        PO2.  I USE ACTUAL ART PO2 CALC. FROM A-A GRAD & ALV PO2.
C     2)  QBN IS TAKEN TO BE 'NORMAL' BRAIN BLOOD FLOW, OF 700 CC/MIN.
C        I USE 15 PCT. OF CARDIAC OUTPUT, FROM SIESJO, ET. AL., 1974).
C        CALCULATE W FROM QBN.
C
C     FACTOR OF 1000 FOR UNIT CONVERSION.  QB IN ML/MIN, QT IN L/MIN
C
C     EMPIRICAL CONSTANTS FROM MILHORN -
C     QBN  IS 'NORMAL' QB = 50 CC/100 GM BRAIN WT/MIN *
C        0.14 GM-L/CC = 0.7 L/MIN = 700 ML/MIN
C     W  IS NO. OF 100 GM INCREMENTS OF BRAIN WT / 1000 = 0.014 GM-L/CC
C     G  IS 'NORMAL' ART PO2 = 98
C
 1900 QBN = BRPCT * Q * 1000.0
      W = QBN / 50000.0
      SUM = A PCO2 * (A PCO2 * (A PCO2 * (A PCO2 * (APCO2 * H + I) + J)
     A   + P) + QQ) + R
      QB = W * (SUM + F * (G - 1.0 / A PO2) ** S) + QBN
      QT = Q - QB / 1000.0
      IFLAG (48) = 3
      IFLAG (49) = 3
      GO TO 9900
C
C***************************************
C     SUBROUTINE FICK      CHART 20
C     FICK EQUATION, FROM WEST, 1974, AND VENOUS ADMIXTURE (PHYSIOLOGIC
C       SHUNT) FROM MITHOEFER.
C
C     Q = VO2 / (ART O2 CONC - MIXED VENOUS O2 CONCENTRATION)
C     SHUNT = (IDEAL O2 CON-ART O2 CON) / (IDEAL O2 CON-XVEN O2 CON)
C
C     APPARENT PO2 MUST BE CORRECTED, A LA KELMAN.
C
 2000 XV CON = A O2 TOT - VO2 / (Q * 10.0)
      CALL BL O2 (APP,TEMP,O2 CAP,SO2,O2 D,O2 C,XV CON,V PH,V PCO2,2)
      XV PO2 = APP / (10.0 ** (0.40 * (V PH - 7.4) +
     A   0.024 * (37.0 - TEMP) + 0.06 * ALOG10 (40.0 / V PCO2)))
      IFLAG (50) = 3
C
      CALL O2 DISS (SO2,A PH,PAO2,TEMP,A PCO2,1)
      CALL BL O2 (PAO2,TEMP,O2 CAP,SO2,O2 D,O2 C,IDEAL C,0.0,0.0,1)
      SHUNT = 100.0 * (IDEAL C - A O2 TOT) / (IDEAL C - XV CON)
      IFLAG (57) = 3
      GO TO 9900
C
C***************************************
C     SUBROUTINE HB HCT       CHART 24
C     HEMOGLOBIN-HEMATOCRIT RELATIONSHIP.  IN NORMAL PEOPLE, HB IS
C        33 - 38 PCT. OF THE HCT.  WE USE A FACTOR OF 3, FROM MERCK
C        MANUAL (PG. 1835).
C
C     HCT = HGB * 3
C
 2400 IF (NKNOWN .EQ. 2)  GO TO 2420
      HCT = HGB * 3.0
      IFLAG (30) = 3
      GO TO 9900
C
 2420 HGB = HCT / 3.0
      IFLAG (33) = 3
      GO TO 9900
C
C***************************************
C     SUBROUTINE CO2CHART 25
C     HANDLE EQUATIONS OF BLOOD CO2 CONTENTS.
C
C     CO2 CONC = F (PCO2,PH,TEMP,HCT,SO2)
C
C     FROM FICK EQN (WEST & WAGNER, 1977) -
C
C     VCO2 = Q * (MIXED VENOUS CO2 CONC - ART CO2 CONC)
C
 2500 CALL BL CO2 (A PCO2,A PH,A SO2,HCT,TEMP,A CO2 T,1)
      XV CO2 T = A CO2 T + VCO2 / (Q * 10.0)
*
* FAHRI sets the Venous pH. If the pH is positive, it will be
* calculated, if negative on entry, it will be made positive.
*
      CALL FARHI
      CALL BL CO2 (V PCO2,V PH,V SO2,HCT,TEMP,XV CO2 T,2)
      CALL BL CO2 (GV PCO2,V PH,95.0, HCT,TEMP,XV CO2 T,2)
      IFLAG (58) = 3
      IFLAG (59) = 3
      IFLAG (60) = 3
      IFLAG (71) = 3
      GO TO 9900
C
C***************************************
C     SUBROUTINE VISCOS       CHART 26  (6/30/80)
C     VISCOSITY-HEMATOCRIT RELATIONSHIP.  FROM ZINGG, ET. AL., 1970.
C
C     VISCOSITY (NU) = 1.24 * E ** (0.02471 * HCT)
C
 2600 IF (NKNOWN .EQ. 2)  GO TO 2620
      VISCOS = 1.24 * EXP (0.02471 * HCT)
      IFLAG (14) = 3
      GO TO 9900
C
 2620 HCT = ALOG (VISCOS / 1.24) / 0.02471
      IFLAG (30) = 3
C
 9900 RETURN
      END
