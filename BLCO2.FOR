$STORAGE:2
      SUBROUTINE BL CO2 (PCO2,PH,SO2,HCRIT,TEMP,CO2B,NKNOWN)
C
C     SUBROUTINE BLOOD CO2
C     CALCULATE BLOOD CO2 CONCENTRATIONS.  MOD. FROM KELMAN, RESP.
C        PHYSIOL., 3.111 (1967).
C
C     CORRECT VALUES FOR BTPS
C
      HC = HCRIT / 100.0
      PHC = 7.4 - PH
      TEMPC = 37.0 - TEMP
C
C     CO2 CONTENT PLASMA.  SOL IS SOL. OF CO2 IN PLASMA
C
      SOL = 0.0307 + (0.00057 + 0.00002 * TEMPC) * TEMPC
      PK = 6.086 + 0.042 * PHC + (38.0 - TEMP) *
     A   (0.00472 + 0.00139 * PHC)
C
C     CO2 CONTENT CELL TO PLASMA RATIO
C
      RR = 0.664 + (0.2275 - 0.0938 * PHC) * PHC
      OR = 0.590 + (0.2913 - 0.0844 * PHC) * PHC
      D = OR + (RR - OR) * (1.0 - SO2 / 100.0)
      IF (NKNOWN .EQ. 2)  GO TO 20
C
C     MISSING CO2 BLOOD CONCENTRATION
C
      CO2P = SOL * PCO2 * (1.0 + 10.0 ** (PH - PK))
      CO2B = (HC * D + (1.0 - HC)) * CO2P * 2.22
      GO TO 9900
C
C     MISSING PCO2
C
   20 CO2P = CO2B / (2.22 * (HC * D + (1.0 - HC)))
      PCO2 = CO2P / (SOL * (1.0 + 10.0 ** (PH - PK)))
C
 9900 RETURN
      END
